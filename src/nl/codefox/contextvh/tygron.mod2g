use knowledgebase as knowledge.
use tygron as actionspec.
use buyLand as module.
use sellLand as module.
use construct as module.
use demolish as module.
use upgrade as module.


exit=nogoals.
order = linearall.

% The main module
module tygron {
	%______________________Execute Action Modules if there is a corresponding goal______________________%
	% build on a MultiPolygon
	if goal (constructBuilding(MultiPoly)) 
		then construct(MultiPoly).
		
	% demolish a building with given ID	
	if goal (demolishBuilding(BuildingID)) 
		then demolish(BuildingID).
			
	% attempt buying land at given MultiPolygon ('attempt' means trying three times or succeeding)	
	if goal (doneBuying(MultiPoly)) 
		then buyLand(MultiPoly).
		
	% attempt selling land at given MultiPolygon ('attempt' means trying three times or succeeding)		
	if goal (doneSelling(MultiPoly)) 
		then sellLand(MultiPoly).
		
	% upgrade a building
	if goal (upgradeBuilding(UpgradeID, MultiPolygon)) 
		then upgrade(UpgradeID, MultiPolygon). 
	%__________________________________________________________________________________________________%
	
	%_______________________________Adopt goals based on the environment_______________________________%
	
	% Adopt goal to build where a demolished building was previously located. 	
	forall bel(emptyPoly(MultiPoly)) 
		do adopt(constructBuilding(MultiPoly)).
	
	% Adopt goals to improve zones for which the agent has not achieved it's target values'	
	forall bel(zones(List), indicator(Indicator,_,_,_), member(zone(Zone, _, _, _, _), List), needImprovement(Indicator, Zone)) 
		do adopt(improveZone(Indicator, Zone)).
		
	% Example of buying and demolish on ground that we just bought. 
	if bel(building(BuildingId, 'Overige functie (1218) verkocht', _, _, _, _, _, MultiPoly), ownedBuilding(BuildingId)) 
		then adopt(demolishBuilding(BuildingId)).	
			
	% Example of buying and demolish on ground that we just bought.
	if bel(building(BuildingId, 'Overige functie (1218)', _, _, _, _, _, MultiPoly), landOfOthers(MultiPoly), not(doneBuying(MultiPoly))) 
		then adopt(doneBuying(MultiPoly)).
			
	% Example of a building we will demolish	
	if bel(building(BuildingId,'Prof. Henketstraat 48-80',_,_,_,_,_,_), ownedBuilding(BuildingId)) 
		then adopt( demolishBuilding(BuildingId)).
	
	% Example of building we will upgrade
	if bel(building(BuildingId,'Prof. Henketstraat 49-85',_,_,_,FunctionID,_,MultiPolygon), ownedBuilding(BuildingId), upgrade_type(UpgradeID,FunctionID,_)) 
		then adopt(upgradeBuilding(UpgradeID, MultiPolygon)).
	 
	% Adtop goal to demolish social buildings when we want more luxury or normal buildings 
	if bel(indicator(34,_, _, ZoneLinkList), member(zone_link(2,_,Current3,Target3), ZoneLinkList), Current3 < Target3, member(zone_link(1,_,Current2,Target2), ZoneLinkList), member(zone_link(0,_,Current1,Target1), ZoneLinkList), (Current1 < Target1 ; Current2 < Target2), building(BuildingId,_,_,_,['SOCIAL'],_,_,_), ownedBuilding(BuildingId) ) 
		then adopt (demolishBuilding(BuildingId)).
	%__________________________________________________________________________________________________%
	
}
