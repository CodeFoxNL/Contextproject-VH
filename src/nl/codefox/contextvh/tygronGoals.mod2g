use knowledgebase as knowledge.

order = linearall.
module tygronGoals {
	%_______________________________Adopt goals based on the environment_______________________________%
	
	%%% Selling buildings
	
	% If we want to sell a building but failed to do it before, we demolish it.
	if bel(building(BuildingID,Name,OwnerID,_,Categories,FunctionID,_,MultiPoly,_,_),ourID(OwnerID),not(housingBuildings(Categories)),
		failedToSell(MultiPoly))
		then adopt(demolishBuilding(BuildingID)).
		
	% Sell all buildings that we own that are not part of housing
	if bel(building(BuildingID,Name,OwnerID,_,Categories,FunctionID,_,MultiPoly,_,_),ourID(OwnerID),not(housingBuildings(Categories)))
		then adopt(doneSelling(MultiPoly)).

	% Adopt goal to build where a demolished building was previously located.
	forall bel(emptyPoly(MultiPoly))
		do adopt(constructBuilding(MultiPoly)).
	
	% Adopt goals to improve zones for which the agent has not achieved it's target values'
	forall bel(needImprovement(IndicatorID,Zone))
		do adopt(improvedZone(IndicatorID,Zone)).
	
	% Upgrade a building when the indicator it improves still needs improvement
	%% List of available upgrades for our buildings
		%UpgradeID/UpgradeDescription/example affected indicators
		%0/Woningen Renoveren/
			%3,SpatialQuality,+0.0034
			%6,Budget,-156144
			%34,Surface,+0,0036
		%41/Zonnepanelen/
			%6,Budget,-3318
			%34,Surface,-0.002
	if bel(UpgradeID is 0,upgrade_type(UpgradeID,UpgradeTypes),
		random_member(upgrade_pair(FunctionId,_),UpgradeTypes),
		building(BuildingId,_,_,_,_,FunctionId,_,_,_,ZoneId),
		zoneToRenovate(ZoneId),
		ownedBuilding(BuildingId)) 
		then adopt(upgradeBuilding(UpgradeID,BuildingId)).
					
	% Adopt goals to demolish social buildings when we want more luxury or normal buildings
	if bel(housingIndicator(IndicatorID),indicator(IndicatorID,_,_,ZoneLinkList),member(zone_link(2,_,Current3,Target3),ZoneLinkList),Current3<Target3,
		member(zone_link(1,_,Current2,Target2),ZoneLinkList),member(zone_link(0,_,Current1,Target1),ZoneLinkList),
		(Current1<Target1;Current2<Target2),building(BuildingId,_,_,_,['SOCIAL'],_,_,_,_,_),ownedBuilding(BuildingId),
		not(alreadyUpgraded(BuildingId)),not(upgraded(BuildingId))),not(goal(upgradeBuilding(_,BuildingId)))
		then adopt (demolishBuilding(BuildingId)).
		
	%%% Examples of actions
	% Example of buying and demolish on ground that we just bought.
	if bel(building(BuildingID,'Overige functie (1218) verkocht',_,_,_,_,_,MultiPoly,_,_),ownedBuilding(BuildingID))
		then adopt(demolishBuilding(BuildingID)).
	% Example of buying and demolish on ground that we just bought.
	if bel(building(BuildingID,'Overige functie (1218)',_,_,_,_,_,MultiPoly,_,_),landOfOthers(MultiPoly),not(doneBuying(MultiPoly)))
		then adopt(doneBuying(MultiPoly)).
	% Example of a building we will demolish
	if bel(building(BuildingID,'Prof. Henketstraat 48-80',_,_,_,_,_,_,_,_),ownedBuilding(BuildingID))
		then adopt(demolishBuilding(BuildingID)).	

	%__________________________________________________________________________________________________%
}